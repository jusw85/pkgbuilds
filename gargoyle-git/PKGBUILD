# Maintainer: Justin Wong <jusw85 at hotmail dot com>

pkgname=gargoyle-git
_pkgname=garglk
pkgver=2019.1.r201.g11c2f68
pkgrel=1
pkgdesc='Interactive Fiction multi-interpreter that supports all major IF formats (development version)'
arch=('x86_64' 'i686')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('custom')
depends=('qt5-base' 'hicolor-icon-theme' 'sdl2_mixer')
makedepends=('git' 'cmake')
conflicts=('gargoyle')
source=("git+https://github.com/${_pkgname}/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
    mkdir -p "${srcdir}/build"
}

build() {
    cd "${srcdir}/build"
    cmake "../${_pkgname}" -DWITH_TTS=false -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/${_pkgname}"
    install -Dm644 "License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"

    cd "${srcdir}/${_pkgname}/${_pkgname}"
    install -Dm644 "garglk.ini" "${pkgdir}/etc/garglk.ini"
    install -Dm644 "interactive-fiction.xml" "${pkgdir}/usr/share/mime/packages/gargoyle.xml"
    install -Dm644 "gargoyle.desktop" "${pkgdir}/usr/share/applications/gargoyle.desktop"
    install -Dm644 "gargoyle-house.png" "${pkgdir}/usr/share/icons/hicolor/32x32/apps/gargoyle-house.png"
}
